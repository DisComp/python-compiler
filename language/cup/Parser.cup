package com.language.parser;

import java.util.*;
import java.util.ArrayList;
import java_cup.runtime.*;
import com.language.model.expression.*;
import com.language.exceptions.*;
import com.language.controllers.*;

/* Numbers */
terminal String INTEGER, FLOAT, LONG_INT;

/* Arithmetic operators */
terminal String PLUS, MINUS, TIMES, EXP, DIV, DIV_INT, MOD;

/* Bitwise operators */
terminal String AND_BIT, OR_BIT, XOR_BIT, NOT_BIT, LSHIFT, RSHIFT;

/* Strings */
terminal String STRING, ESCAPE,DEDENT, IDENT, SYNERROR,LINETERMINATOR;

/* Boolean */
terminal String TRUE, FALSE;

/* Boolean operators */
terminal String AND, OR, NOT, EQUALS, DISTINCT, LESS, GREATER, LESS_EQUAL, GREATER_EQUAL;

/* Punctuation chars */
terminal String POINT, SEMICOLON, COMMA, COLON, LPAREN, RPAREN, LBRACE, RBRACE, LBRACKET, RBRACKET, ASSIGN;

/* Structure control */
terminal String IF, ELSE, WHILE, BREAK, CONTINUE, FOR, IN, DEF, RETURN;

/* Predefined functions */
	
/* Dictionaries */
terminal String HAS_KEY_FUNC, ITEMS_FUNC, KEYS_FUNC, POP_FUNC, VALUES_FUNC;
	
/* Strings */
terminal String COUNT_FUNC, FIND_FUNC, JOIN_FUNC, SPLIT_FUNC, REPLACE_FUNC, LENGTH_FUNC;

/* Lists */
terminal String APPEND_FUNC, EXTEND_FUNC, INDEX_FUNC, INSERT_FUNC, SIZE_FUNC;
	
/* Input/Output */
terminal String RAW_INPUT_FUNC, PRINT_FUNC;

/* Type conversion */
terminal String INT_FUNC, FLOAT_FUNC, STR_FUNC, TUPLE_FUNC, LIST_FUNC, DICT_FUNC;

/* Long */
terminal String LONG_FUNC;

/* String */
terminal String STRING_FUNC;
	
/* Others */
terminal String NONE; 
terminal String TYPE_FUNC; 
terminal String ID;

/* Line Terminator*/
//terminal String LINE_TERMINATOR;

/* NON TERMINALS */
non terminal Expression expr;
non terminal Expression instr;
non terminal Expression function_block;
non terminal Expression assignment_expr;
non terminal Expression left_side_expr;
non terminal Expression structured_types_access;
non terminal Expression atom_structured_types_access;
non terminal Expression indent_block;
non terminal Expression indent_instrs;
non terminal Expression if_block;
non terminal Expression while_block;

non terminal Expression predefined_functions_expr;
non terminal Expression predefined_dictionary_functions;
non terminal Expression predefined_string_functions;
non terminal Expression predefined_list_functions;
non terminal Expression predefined_io_functions;
non terminal Expression predefined_type_conversion_functions;

non terminal Expression boolean_expr;
non terminal Expression bitwise_expr;
non terminal Expression arithmetical_expr;
non terminal Expression parenthesized_expr;
non terminal Expression list_expr;
non terminal Expression element_list;
non terminal Expression dictionary_expr;
non terminal Expression element_dictionary;
non terminal Expression atom_element_dictionary;
non terminal Expression last_atom_element_dictionary;
non terminal Expression tuple_expr;
non terminal Expression element_tuple;
non terminal Expression atom_element_tuple;
non terminal Expression literal_expr;

non terminal Expression function_call;
non terminal List<String> parameters;
non terminal List<String> individual_parameters;
non terminal List<Expression> call_parameters;
non terminal List<Expression> call_individual_parameters;

non terminal Expression stmt;
non terminal Expression compl_stmt;
non terminal Expression indent_ins;
non terminal Expression suit;
non terminal Expression suit_func;
non terminal Expression program;


precedence left OR;
precedence left AND;
precedence left NOT;
precedence left EQUALS, DISTINCT, LESS, GREATER, LESS_EQUAL, GREATER_EQUAL;
precedence left OR_BIT;
precedence left XOR_BIT;
precedence left AND_BIT;
precedence left LSHIFT, RSHIFT;
precedence left PLUS, MINUS;
precedence left TIMES, DIV, DIV_INT, MOD;
precedence left NOT_BIT;
precedence left EXP;
precedence left ASSIGN;
//precedence left TAB;
precedence left IF;
//precedence left LINE_TERMINATOR; 
precedence left STRING;
precedence left ID;

start with program;

program ::= stmt: s 
			{: 
				s.execute();
				RESULT = s;
			:};

stmt ::=  compl_stmt:c stmt:s {: RESULT = StatementExpression.create(StatementExpression.STMT, c, s); :}
		| instr:i LINETERMINATOR stmt:s {: RESULT = StatementExpression.create(StatementExpression.STMT, i, s); :}
		| compl_stmt:c {: RESULT = StatementExpression.create(StatementExpression.STMT, c, null); :}
		| instr:i LINETERMINATOR{: RESULT = StatementExpression.create(StatementExpression.STMT, i, null); :};
	
suit::= IDENT stmt:s DEDENT{: RESULT = s; :};

compl_stmt ::= if_block: iff {: RESULT = iff; :}
				|function_block:fb {: RESULT = fb; :};	 

indent_ins ::= IDENT stmt:s {: RESULT = s; :}
			|  IDENT stmt:s indent_ins:iis {: s.setRight(iis); RESULT = s; :};	
	

				 
instr ::= expr: exp {: RESULT = exp; :}		
		| assignment_expr: v {: RESULT = v; :};


/*Function*/		
function_call::= ID:id LPAREN call_parameters:cp RPAREN {: RESULT = FunctionCallExpression.create(id,cp); :};
call_parameters ::= {: RESULT = new ArrayList<Expression>(); :}//sin parámetros
			|call_individual_parameters:ip{: RESULT = ip; :};//con al menos un parámetro	

call_individual_parameters ::=ID: id {: 	List<Expression> l = new ArrayList<Expression>();
									l.add(new Expression());
									RESULT = l; :}
						| ID: id COMMA call_individual_parameters:iparams {:	iparams.add(new Expression());RESULT = iparams;:} ;	
																		
function_block ::=	DEF ID: funct_name
					LPAREN parameters:params RPAREN 
					COLON suit_func:s {: RESULT = FunctionExpression.create(funct_name,s,params); :};
					
parameters ::= {: RESULT = new ArrayList<String>(); :}//sin parámetros
			|individual_parameters:ip{: RESULT = ip; :};//con al menos un parámetro	

individual_parameters ::=ID: id {: 	List<String> l = new ArrayList<String>();
									l.add(id);
									RESULT = l; :}
						| ID: id COMMA individual_parameters:iparams {:	if(iparams.contains(id))
																				throw new ParsingException("El parametro "+id+" ya fue definido");
																		else 	
																			iparams.add(id);
																		RESULT = iparams;:} ;	
																			
suit_func::= IDENT stmt:s RETURN expr:e LINETERMINATOR DEDENT{:	//cuerpo con return
													Expression exp = new Expression();
													exp.setLeft(s);
													exp.setRight(e);
													RESULT = exp; :}
	
			|IDENT RETURN expr:e LINETERMINATOR DEDENT{:	//solo return
													Expression exp = new Expression();
													exp.setLeft(null);
													exp.setRight(e);
													RESULT = exp; :}
			|IDENT stmt:s DEDENT{:	//cuerpo sin return
									Expression exp = new Expression();
									exp.setLeft(s);
									exp.setRight(null);
									RESULT = exp; :};
	

				


/* If block */
if_block ::= IF expr: e COLON suit: iis {: RESULT = StructureControlExpression.create(StructureControlExpression.IF, "if", e, iis); :};

/* While block */
while_block ::= WHILE expr: e COLON suit: iis {: RESULT = StructureControlExpression.create(StructureControlExpression.WHILE, "while", e, iis); :};


assignment_expr ::=   left_side_expr: lse ASSIGN expr: e {:	RESULT = LiteralExpression.createAssignment(lse, e); :};  

left_side_expr ::= ID: id {: RESULT = LiteralExpression.createIdentifier(id); :}
				|  atom_structured_types_access: asta {: RESULT = asta; :};  // other things that can be assigned e.g lists, dicts

								/* We need to extract the variable value from the java code */
predefined_functions_expr ::= literal_expr: le POINT predefined_dictionary_functions: pdf {: RESULT =  PredefinedFunctionExpression.createDictionaryFunction( le, pdf); :}
							| ID: id POINT predefined_string_functions: psf {: RESULT =  PredefinedFunctionExpression.createStringFunction(LiteralExpression.createIdentifier(id), psf); :}
							| ID: id POINT predefined_list_functions: plf {: RESULT =  PredefinedFunctionExpression.createListFunction(LiteralExpression.createIdentifier(id), plf); :}
							| predefined_io_functions: piof {: RESULT =  piof; :}
							| predefined_type_conversion_functions: piof {: RESULT =  piof; :}
							| TYPE_FUNC LPAREN literal_expr: le RPAREN {: RESULT = PredefinedFunctionExpression.createEvaluateTypeFunction(le); :}
							
								// functions used for multiple objects
							| ID: id POINT POP_FUNC LPAREN literal_expr: le RPAREN {: RESULT = PredefinedFunctionExpression.createPopFunction(LiteralExpression.createIdentifier(id), le); :}
							| ID: id POINT COUNT_FUNC LPAREN literal_expr:le RPAREN {: RESULT = PredefinedFunctionExpression.createCountFunction(LiteralExpression.createIdentifier(id), le); :};

predefined_dictionary_functions ::=   HAS_KEY_FUNC LPAREN literal_expr: le RPAREN {: RESULT = PredefinedFunctionExpression.createDictionaryFunctionElement(PredefinedFunctionExpression.HAS_KEY_FUNC, le); :}
									| ITEMS_FUNC LPAREN RPAREN {: RESULT = PredefinedFunctionExpression.createDictionaryFunctionElement(PredefinedFunctionExpression.ITEMS_FUNC, null); :}
									| KEYS_FUNC LPAREN RPAREN {: RESULT = PredefinedFunctionExpression.createDictionaryFunctionElement(PredefinedFunctionExpression.KEYS_FUNC, null); :}									
									| VALUES_FUNC LPAREN RPAREN {: RESULT = PredefinedFunctionExpression.createDictionaryFunctionElement(PredefinedFunctionExpression.VALUES_FUNC, null); :};


predefined_string_functions ::= FIND_FUNC LPAREN literal_expr: le RPAREN {: RESULT = PredefinedFunctionExpression.createStringFunctionElement(PredefinedFunctionExpression.FIND_FUNC, le); :}
							|   FIND_FUNC LPAREN literal_expr: le_s COMMA literal_expr: le_i RPAREN {: RESULT = PredefinedFunctionExpression.createStringFunctionElement(PredefinedFunctionExpression.FIND_FUNC, le_s, le_i); :}
							|   JOIN_FUNC LPAREN literal_expr: le RPAREN {: RESULT = PredefinedFunctionExpression.createStringFunctionElement(PredefinedFunctionExpression.JOIN_FUNC, le); :}
							|   SPLIT_FUNC LPAREN literal_expr: le RPAREN {: RESULT = PredefinedFunctionExpression.createStringFunctionElement(PredefinedFunctionExpression.SPLIT_FUNC, le); :}
							|   REPLACE_FUNC LPAREN literal_expr: le_o COMMA literal_expr: le_n RPAREN {: RESULT = PredefinedFunctionExpression.createStringFunctionElement(PredefinedFunctionExpression.REPLACE_FUNC, le_o, le_n); :}
							|   LENGTH_FUNC LPAREN  RPAREN {: RESULT = PredefinedFunctionExpression.createStringFunctionElement(PredefinedFunctionExpression.LENGTH_FUNC, null); :};


predefined_list_functions ::= APPEND_FUNC LPAREN literal_expr:le RPAREN {: RESULT = PredefinedFunctionExpression.createListFunctionElement(PredefinedFunctionExpression.APPEND_FUNC, le); :}
							| EXTEND_FUNC LPAREN literal_expr: le RPAREN {: RESULT = PredefinedFunctionExpression.createListFunctionElement(PredefinedFunctionExpression.EXTEND_FUNC, le); :}
							| INDEX_FUNC LPAREN literal_expr:le RPAREN {: RESULT = PredefinedFunctionExpression.createListFunctionElement(PredefinedFunctionExpression.INDEX_FUNC, le); :}
							| INDEX_FUNC LPAREN literal_expr:le COMMA literal_expr: le_i RPAREN {: RESULT = PredefinedFunctionExpression.createListFunctionElement(PredefinedFunctionExpression.INDEX_FUNC, le, le_i); :}
							| INSERT_FUNC LPAREN literal_expr: le_i COMMA literal_expr:le RPAREN {: RESULT = PredefinedFunctionExpression.createListFunctionElement(PredefinedFunctionExpression.INDEX_FUNC, le_i, le ); :}
							| SIZE_FUNC LPAREN RPAREN {: RESULT = PredefinedFunctionExpression.createListFunctionElement(PredefinedFunctionExpression.SIZE_FUNC, null); :};

predefined_io_functions ::= RAW_INPUT_FUNC LPAREN literal_expr: le RPAREN {: RESULT = PredefinedFunctionExpression.createIOFunction(PredefinedFunctionExpression.RAW_INPUT_FUNC, le); :}
						|   PRINT_FUNC literal_expr:le {: RESULT = PredefinedFunctionExpression.createIOFunction(PredefinedFunctionExpression.PRINT_FUNC, le); :};

						
predefined_type_conversion_functions ::=  INT_FUNC LPAREN literal_expr: le RPAREN {: RESULT = PredefinedFunctionExpression.createTypeConversionFunction(PredefinedFunctionExpression.INT_FUNC, le); :}
										| FLOAT_FUNC LPAREN literal_expr: le RPAREN {: RESULT = PredefinedFunctionExpression.createTypeConversionFunction(PredefinedFunctionExpression.FLOAT_FUNC, le); :}
										| STR_FUNC LPAREN literal_expr: le RPAREN {: RESULT = PredefinedFunctionExpression.createTypeConversionFunction(PredefinedFunctionExpression.STR_FUNC, le); :}
										| TUPLE_FUNC LPAREN literal_expr: le RPAREN {: RESULT = PredefinedFunctionExpression.createTypeConversionFunction(PredefinedFunctionExpression.TUPLE_FUNC, le); :}
										| LIST_FUNC LPAREN literal_expr: le RPAREN {: RESULT = PredefinedFunctionExpression.createTypeConversionFunction(PredefinedFunctionExpression.LIST_FUNC, le); :}
										| DICT_FUNC LPAREN literal_expr: le RPAREN {: RESULT = PredefinedFunctionExpression.createTypeConversionFunction(PredefinedFunctionExpression.DICT_FUNC, le); :};
										
						
/* The precedence starts with boolean_expr */
boolean_expr ::=  boolean_expr: l OR  boolean_expr: r {: RESULT = BooleanExpression.create(BooleanExpression.OR, "or", l, r); :}
				| boolean_expr: l AND boolean_expr: r {: RESULT = BooleanExpression.create(BooleanExpression.AND, "and", l, r); :}
				| NOT boolean_expr: l {: RESULT = BooleanExpression.create(BooleanExpression.NOT, "not", l); :}
				| boolean_expr: l EQUALS boolean_expr: r {: RESULT = BooleanExpression.create(BooleanExpression.EQUALS, "==", l, r); :}
				| boolean_expr: l DISTINCT boolean_expr: r {: RESULT = BooleanExpression.create(BooleanExpression.DISTINCT, "!=", l, r); :}
				| boolean_expr: l LESS boolean_expr: r {: RESULT = BooleanExpression.create(BooleanExpression.LESS, "<", l, r); :}
				| boolean_expr: l GREATER boolean_expr: r {: RESULT = BooleanExpression.create(BooleanExpression.GREATER, ">", l, r); :}
				| boolean_expr: l LESS_EQUAL boolean_expr: r {: RESULT = BooleanExpression.create(BooleanExpression.LESS_EQUAL, "<=", l, r); :}
				| boolean_expr: l GREATER_EQUAL boolean_expr: r {: RESULT = BooleanExpression.create(BooleanExpression.GREATER_EQUAL, ">=", l, r); :}
				| bitwise_expr: x {: RESULT = x; :};	

bitwise_expr ::=    bitwise_expr: l OR_BIT  bitwise_expr: r {: RESULT = BitwiseExpression.create(BitwiseExpression.OR_BIT, "|", l, r); :}
				  | bitwise_expr: l XOR_BIT bitwise_expr: r {: RESULT = BitwiseExpression.create(BitwiseExpression.XOR_BIT, "^", l, r); :}
				  | bitwise_expr: l AND_BIT bitwise_expr: r {: RESULT = BitwiseExpression.create(BitwiseExpression.AND_BIT, "&", l, r); :}
				  | bitwise_expr: l LSHIFT  bitwise_expr: r {: RESULT = BitwiseExpression.create(BitwiseExpression.LSHIFT, "<<", l, r); :}
                  | bitwise_expr: l RSHIFT  bitwise_expr: r {: RESULT = BitwiseExpression.create(BitwiseExpression.RSHIFT, ">>", l, r); :}
                  | NOT_BIT  bitwise_expr: l {: RESULT = BitwiseExpression.create(BitwiseExpression.NOT_BIT, "~", l); :}
				  | arithmetical_expr: x {: RESULT = x; :};				  

arithmetical_expr ::=    arithmetical_expr: l PLUS    arithmetical_expr: r {: RESULT = ArithmeticalExpression.create(ArithmeticalExpression.PLUS, "+", l, r); :}
				       | arithmetical_expr: l MINUS   arithmetical_expr: r {: RESULT = ArithmeticalExpression.create(ArithmeticalExpression.MINUS, "-", l, r); :}
					   | arithmetical_expr: l TIMES   arithmetical_expr: r {: RESULT = ArithmeticalExpression.create(ArithmeticalExpression.TIMES, "*", l, r); :} 
				       | arithmetical_expr: l DIV     arithmetical_expr: r {: RESULT = ArithmeticalExpression.create(ArithmeticalExpression.DIV, "/", l, r); :}
				       | arithmetical_expr: l DIV_INT arithmetical_expr: r {: RESULT = ArithmeticalExpression.create(ArithmeticalExpression.DIV_INT, "//", l, r); :}
				       | arithmetical_expr: l MOD     arithmetical_expr: r {: RESULT = ArithmeticalExpression.create(ArithmeticalExpression.MOD, "%", l, r); :}
                       | arithmetical_expr: b EXP     arithmetical_expr: e {: RESULT = ArithmeticalExpression.create(ArithmeticalExpression.EXP, "**", b, e); :}
					   | literal_expr: x {: RESULT = x; :};
					
parenthesized_expr ::= LPAREN boolean_expr:be RPAREN {: RESULT = be; :};
    
list_expr ::= LBRACKET element_list:el RBRACKET {: RESULT = el; :}
    		| LBRACKET RBRACKET {: RESULT = LiteralExpression.createListElement(null, null); :};
    		
element_list ::=  literal_expr:le COMMA element_list:el {: RESULT = LiteralExpression.createListElement(le, el); :}
				| literal_expr:le {: RESULT = le; :};

dictionary_expr ::= LBRACE element_dictionary:ed RBRACE {: RESULT = ed; :}
				|	LBRACE IDENT element_dictionary:ed DEDENT RBRACE {: RESULT = ed; :}
				|   LBRACE RBRACE {: RESULT = LiteralExpression.createDictionaryElement(null, null); :};
    		
element_dictionary ::= atom_element_dictionary:atd element_dictionary:ed {: RESULT = LiteralExpression.createDictionaryElement(atd, ed); :}
					|  last_atom_element_dictionary:laed {: RESULT = laed; :};

atom_element_dictionary ::= literal_expr:key COLON literal_expr:val COMMA {: RESULT = LiteralExpression.createAtomDictionaryElement(key, val); :};

last_atom_element_dictionary ::= literal_expr:key COLON literal_expr:val COMMA {: RESULT = LiteralExpression.createAtomDictionaryElement(key, val); :}
							|    literal_expr:key COLON literal_expr:val {: RESULT = LiteralExpression.createAtomDictionaryElement(key, val); :};


tuple_expr ::= LPAREN element_tuple:et RPAREN {: RESULT = et; :}
			|  LPAREN RPAREN {: RESULT = LiteralExpression.createTupleElement(null, null); :};
    		
element_tuple ::= literal_expr:le atom_element_tuple:aet {: RESULT = LiteralExpression.createTupleElement(le, aet); :};

atom_element_tuple ::= COMMA literal_expr: le atom_element_tuple: aet {: RESULT = LiteralExpression.createTupleElement(le, aet); :}
					|  COMMA literal_expr: le {: RESULT = le; :}
					|  COMMA;
					
// Access to lists, dictionaries and tuples elements. e.g: ( a[0], a[0:1:2], a[::2]	)
structured_types_access ::=  ID: id LBRACKET literal_expr: init COLON literal_expr: end COLON literal_expr: jump RBRACKET {: RESULT = LiteralExpression.createStructureAccess(LiteralExpression.createIdentifier(id), init, end, jump); :}
						|    ID: id LBRACKET COLON literal_expr: end COLON literal_expr: jump RBRACKET {: RESULT = LiteralExpression.createStructureAccess(LiteralExpression.createIdentifier(id), null, end, jump); :}
						|    ID: id LBRACKET literal_expr: init COLON COLON literal_expr: jump RBRACKET {: RESULT = LiteralExpression.createStructureAccess(LiteralExpression.createIdentifier(id), init, null, jump); :}
						|    ID: id LBRACKET literal_expr: init COLON literal_expr: end COLON RBRACKET {: RESULT = LiteralExpression.createStructureAccess(LiteralExpression.createIdentifier(id), init, end, null); :}
						|    ID: id LBRACKET COLON COLON literal_expr: jump RBRACKET {: RESULT = LiteralExpression.createStructureAccess(LiteralExpression.createIdentifier(id), null, null, jump); :}
						|    ID: id LBRACKET COLON literal_expr: end COLON RBRACKET {: RESULT = LiteralExpression.createStructureAccess(LiteralExpression.createIdentifier(id), null, end, null); :}
						|    ID: id LBRACKET literal_expr: init COLON COLON RBRACKET {: RESULT = LiteralExpression.createStructureAccess(LiteralExpression.createIdentifier(id), init, null, null); :}
						|    ID: id LBRACKET COLON COLON RBRACKET {: RESULT = LiteralExpression.createStructureAccess(LiteralExpression.createIdentifier(id), null, null, null); :}
						|    atom_structured_types_access: asta {: RESULT = asta; :};
			
atom_structured_types_access ::=  ID: id LBRACKET literal_expr: init COLON literal_expr: end RBRACKET {: RESULT = LiteralExpression.createStructureAccess(LiteralExpression.createIdentifier(id), init, end); :}
								| ID: id LBRACKET COLON literal_expr: end RBRACKET {: RESULT = LiteralExpression.createStructureAccess(LiteralExpression.createIdentifier(id), null, end); :}
								| ID: id LBRACKET literal_expr: init COLON RBRACKET {: RESULT = LiteralExpression.createStructureAccess(LiteralExpression.createIdentifier(id), init, null); :}
								| ID: id LBRACKET COLON RBRACKET {: RESULT = LiteralExpression.createStructureAccess(LiteralExpression.createIdentifier(id), null, null); :}
             					| ID: id LBRACKET literal_expr: pos RBRACKET {: RESULT = LiteralExpression.createStructureAccess(LiteralExpression.createIdentifier(id), pos); :};

             
literal_expr ::=  ID: 		id {: RESULT = LiteralExpression.createIdentifier(id); :}
				| NONE: 	n  {: RESULT = LiteralExpression.createNone(); :}
				| FALSE:    f  {: RESULT = LiteralExpression.createBoolean(f); :}
				| TRUE:     t  {: RESULT = LiteralExpression.createBoolean(t); :}
				| INTEGER:  i  {: RESULT = LiteralExpression.createInteger(i); :}				
				| FLOAT:    f  {: RESULT = LiteralExpression.createFloat(f); :}
				| LONG_INT: l  {: RESULT = LiteralExpression.createLongInt(l); :}
				| STRING:   s  {: RESULT = LiteralExpression.createString(s); :}
				| parenthesized_expr: p {: RESULT = p; :}
				| list_expr: l {: RESULT = LiteralExpression.createList(l); :}
				| dictionary_expr: l {: RESULT = LiteralExpression.createDictionary(l); :}
				| tuple_expr: l {: RESULT = LiteralExpression.createTuple(l); :}
				| structured_types_access: sta {: RESULT = sta; :};

expr  ::= boolean_expr: b {: RESULT = b; :}
		| predefined_functions_expr: pfe {: RESULT = pfe; :}
		| function_call:fc {: RESULT = fc; :};  
	

